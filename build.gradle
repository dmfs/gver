plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.19.0"
    id "org.dmfs.gitversion" version "0.1.0"
}

apply from: 'jacoco.gradle'

sourceCompatibility = JavaVersion.VERSION_11

gitVersion {
    githubRepo "dmfs/gitversion"
    changes {
        are major when {
            commitMessage contains("(?i)#major\\b")
        }
        are major when {
            commitMessage contains("(?i)#break\\b")
        }
        are minor when {
            commitMessage referencesGithubIssue { issue -> issue.labels?.every { it.name != "bug" } }
        }
        are patch when {
            commitMessage referencesGithubIssue { issue -> issue.labels?.any { it.name == "bug" } }
        }
        are minor when {
            commitMessage contains("#feature\\b")
        }
        are minor when {
            commitMessage contains("(?i)\\b(implement(s|ed)?|close[sd]?) #\\d+\\b")
        }
        are patch when {
            commitMessage contains("(?i)\\b(fix(e[sd])?|resolve[sd]?) #\\d+\\b")
        }
        otherwise preRelease
    }
    releaseBranchPattern ~/main$/
}


repositories {
    mavenCentral()
}

allprojects {
    group 'org.dmfs'
    repositories {
        mavenCentral()
    }
}

dependencies {
    implementation "org.dmfs:semver:0.0.7"
    implementation "org.eclipse.jgit:org.eclipse.jgit:6.0.0.202111291000-r"
    implementation 'org.dmfs:srcless-annotations:0.0.5'
    implementation "org.dmfs:jems2:2.8.0"
    annotationProcessor 'org.dmfs:srcless-processors:0.0.5'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation "org.dmfs:jems2-testing:2.8.0"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testAnnotationProcessor 'org.dmfs:srcless-processors:0.0.5'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        gitVersionPlugin {
            id = 'org.dmfs.gitversion'
            implementationClass = 'org.dmfs.gradle.gitversion.GitVersionPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/dmfs/gitversion'
    vcsUrl = 'https://github.com/dmfs/gitversion'

    plugins {
        gitVersionPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'GitVersion plugin'
            description = 'Gradle versioning based on git history.'
            tags = ['semver', 'tag', 'git', 'version', 'semantic-versioning', 'semantic']
        }
    }
}
