plugins {
    id "org.dmfs.gitversion" version "0.14.1"
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

gitVersion {
    issueTracker GitHub {
        repo = "dmfs/gitversion"
        if (project.hasProperty("GITHUB_API_TOKEN")) {
            accessToken = GITHUB_API_TOKEN
        }
    }
    changes {
        are none when {
            affects only(matches(~/.*\.md/))
        }
        are major when {
            commitMessage contains("(?i)#major\\b")
        }
        are major when {
            commitMessage contains("(?i)#break\\b")
        }
        are minor when {
            commitMessage contains(~/(?i)#(?<issue>\d+)\b/) {
                where("issue") { isIssue { labeled "enhancement" } }
            }
        }
        are patch when {
            commitMessage contains(~/(?i)#(?<issue>\d+)\b/) {
                where("issue") { isIssue { labeled "bug" } }
            }
        }
        are minor when {
            commitMessage contains("#feature\\b")
        }
    }
    releaseBranchPattern ~/main$/
}

if (project.hasProperty('SONATYPE_USERNAME') && project.hasProperty('SONATYPE_PASSWORD')) {
    apply plugin: 'io.github.gradle-nexus.publish-plugin'

    nexusPublishing {
        repositories {
            sonatype {
                username = SONATYPE_USERNAME
                password = SONATYPE_PASSWORD
            }
        }
    }
}

allprojects {
    group 'org.dmfs'
    repositories {
        mavenCentral()
    }
}

gradle.taskGraph.whenReady {
    tasks.withType(PublishToMavenRepository) { PublishToMavenRepository t ->
        if ( t.repository == null ) {
            logger.info( "Task `{}` had null repository", t.path )
        }
        else if ( t.repository.name == "sonatype" ) {
            logger.lifecycle( "Disabling task `{}` because it publishes to Sonatype", t.path )
            t.enabled = false
        }
    }
}