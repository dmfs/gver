plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.19.0"
    id "org.dmfs.gitversion" version "0.7.0"
    id 'maven-publish' // required to generate correct meta data
}

apply from: 'jacoco.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8

gitVersion {
    issueTracker GitHub {
        repo = "dmfs/gitversion"
        if (project.hasProperty("GITHUB_API_TOKEN")) {
            accessToken = GITHUB_API_TOKEN
        }
    }
    changes {
        are major when {
            commitMessage contains("(?i)#major\\b")
        }
        are major when {
            commitMessage contains("(?i)#break\\b")
        }
        are minor when {
            commitMessage containsIssue { issue -> issue.labels?.every { it.name != "bug" } }
        }
        are patch when {
            commitMessage containsIssue { issue -> issue.labels?.any { it.name == "bug" } }
        }
        are minor when {
            commitMessage contains("#feature\\b")
        }
        are minor when {
            commitMessage contains("(?i)\\b(implement(s|ed)?|close[sd]?) #\\d+\\b")
        }
        are patch when {
            commitMessage contains("(?i)\\b(fix(e[sd])?|resolve[sd]?) #\\d+\\b")
        }
    }
    releaseBranchPattern ~/main$/
}

repositories {
    mavenCentral()
}

allprojects {
    group 'org.dmfs'
    repositories {
        mavenCentral()
    }
}

sourceSets {
    main {
        java { srcDirs = [] }    // no source dirs for the java compiler
        groovy { srcDirs = ["src/main/java", "src/main/groovy"] }  // compile everything in src/ with groovy
    }
}
dependencies {
    implementation "org.dmfs:semver:0.1.0"
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.2.0.202206071550-r'
    implementation 'org.dmfs:srcless-annotations:0.1.0'
    implementation "org.dmfs:jems2:2.8.0"
    annotationProcessor 'org.dmfs:srcless-processors:0.1.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testImplementation "org.dmfs:jems2-testing:2.8.0"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.saynotobugs:confidence-core:0.1.0"
    testAnnotationProcessor 'org.dmfs:srcless-processors:0.1.0'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        gitVersionPlugin {
            id = 'org.dmfs.gitversion'
            implementationClass = 'org.dmfs.gradle.gitversion.GitVersionPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/dmfs/gitversion'
    vcsUrl = 'https://github.com/dmfs/gitversion'

    plugins {
        gitVersionPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'GitVersion plugin'
            description = 'Gradle versioning based on git history.'
            tags = ['semver', 'tag', 'git', 'version', 'semantic-versioning', 'semantic']
        }
    }
}
