plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.19.0"
    id 'maven-publish' // required to generate correct meta data
}

apply from: '../jacoco.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":gitversion-dsl")
    implementation "org.dmfs:semver:0.1.0"
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.2.0.202206071550-r'
    implementation 'org.dmfs:srcless-annotations:0.2.0'
    implementation "org.dmfs:jems2:2.14.0"
    annotationProcessor 'org.dmfs:srcless-processors:0.2.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testImplementation "org.dmfs:jems2-testing:2.14.0"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.saynotobugs:confidence-core:0.8.1"
    testAnnotationProcessor 'org.dmfs:srcless-processors:0.2.0'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        gitVersionPlugin {
            id = 'org.dmfs.gitversion'
            implementationClass = 'org.dmfs.gradle.gitversion.GitVersionPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/dmfs/gitversion'
    vcsUrl = 'https://github.com/dmfs/gitversion'

    plugins {
        gitVersionPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'GitVersion plugin'
            description = 'Gradle versioning based on git history.'
            tags = ['semver', 'tag', 'git', 'version', 'semantic-versioning', 'semantic']
        }
    }
}

gradle.taskGraph.whenReady {
    tasks.withType(PublishToMavenRepository) { PublishToMavenRepository t ->
        if (t.repository == null) {
            logger.info("Task `{}` had null repository", t.path)
        } else if (t.repository.name == "sonatype") {
            logger.lifecycle("Disabling task `{}` because it publishes to Sonatype", t.path)
            t.enabled = false
        }
    }
}